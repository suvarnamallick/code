apply plugin: 'war'
apply plugin: 'liberty'

group = 'liberty.gradle'
version = '1'

description = "Project with a simple Servlet application"

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.6.3'
    }
}

repositories {
    mavenCentral()
}

ext {
    // Liberty server properties
    wlpServerName = 'LibertyProjectServer'
    testServerHttpPort = 9080
    testServerHttpsPort = 9443

    // This is set in the ibm-web-ext.xml file
    warContext = 'servlet'
}

war {
    archiveName = baseName + '.' + extension
}

dependencies {
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version:'4.4.6'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version:'4.5.2'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.apache.cxf', name: 'cxf-rt-rs-client', version:'3.1.11'
    testCompile group: 'org.glassfish', name: 'javax.json', version:'1.0.4'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
    libertyRuntime group: 'com.ibm.websphere.appserver.runtime', name: 'wlp-webProfile7', version: '17.0.0.2'
}

liberty {
    server {
        bootstrapProperties = ['default.http.port': testServerHttpPort,
                               'default.https.port': testServerHttpsPort,
                               'appLocation': war.archiveName,
                               'appContext': warContext]
        name = wlpServerName
        apps = [war]
    }
}

test {
    doFirst {
        println 'inside the test block'
    }
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-results/unit")
    exclude '**/it/**'
}

task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    reports.html.destination = file("$buildDir/reports/it")
    reports.junitXml.destination = file("$buildDir/test-results/it")
    include '**/it/**'
    exclude '**/unit/**'

    systemProperties = ['liberty.test.port': testServerHttpPort, 'war.context': warContext]
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(":libertyStart") && !graph.hasTask(":libertyStop")) {
        // Doesn't know if libertyStart succeeds or not
        libertyStart.doLast {
            println ("The server should be running on: http://localhost:${testServerHttpPort}/${warContext}")
        }
    }
}

clean.dependsOn 'libertyStop'
check.dependsOn 'integrationTest'
integrationTest.dependsOn 'libertyStart', 'testClasses'
integrationTest.finalizedBy 'libertyStop'
